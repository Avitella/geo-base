Import("env")

gmock_lib = env.StaticLibrary(
    "gmock",
    [
        "gtest/src/gtest-all.cc",
        "src/gmock-all.cc"
    ],
    CXXFLAGS = [
        env["CXXFLAGS"],
        "-Wno-missing-field-initializers",
        "-DGTEST_LANG_CXX11=0"
    ],
    CPPPATH = [
        "gtest",
        ".",
        "gtest/include",
        "include",
        "gtest/src",
        "src"
    ]
)

gmock_main = env.Object(
    "gmock_main",
    [
        "src/gmock_main.cc"
    ],
    CXXFLAGS = [
        env["CXXFLAGS"],
        "-DGTEST_LANG_CXX11=0"
    ],
    CPPPATH = [
        "gtest/include",
        "include"
    ]
)

path = Dir(".").srcnode().abspath

gmock_include = [
    path + "/include",
    path + "/gtest/include"
]

gmock = {
    "LIBS" : gmock_lib,
    "INCLUDE" : gmock_include,
    "MAIN" : gmock_main,
    "CXXFLAGS" : ["-DGTEST_LANG_CXX11=0"]
}

Return("gmock")
